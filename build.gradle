buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath "io.qameta.allure:allure-gradle:2.5"
        classpath "com.commercehub:gradle-cucumber-jvm-plugin:0.12"
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'com.commercehub.cucumber-jvm'

apply from: "${rootDir}/gradle/allure.gradle"
apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

Closure getMaxParallelForks = {
    int parallelForks = project.hasProperty('parallelForks') ? parallelForks as int : 3
    int processorCores = Runtime.getRuntime().availableProcessors()

    parallelForks > processorCores ? processorCores : parallelForks
}

addCucumberSuite 'test'

cucumber {
    maxParallelForks = getMaxParallelForks()
}

dependencies {
    testImplementation logger_libs
    testImplementation cucumber_libs
    testImplementation allure_libs
    testImplementation guice
    testImplementation junit
    testImplementation selenide
}

test {
    group = "verification"
    description = "Run cucumber tests in parallel"

    stepDefinitionRoots = ['lv/ss/at/cukes/steps']
    featureRoots = ['lv/ss/at/cukes/features']
    tags = project.hasProperty('tags') ? ["${project.properties.tags} and (not @disabled)"] : ['@all and (not @disabled)']
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}