buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath "com.commercehub:gradle-cucumber-jvm-plugin:0.14"
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'com.commercehub.cucumber-jvm'

apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

Closure getMaxParallelForks = {
    int parallelForks = project.hasProperty('parallelForks') ? parallelForks as int : 3
    int processorCores = Runtime.getRuntime().availableProcessors()

    parallelForks > processorCores ? processorCores : parallelForks
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    testImplementation logger_libs
    testImplementation cucumber_libs
    testImplementation allure_libs
    testImplementation guice
    testImplementation junit
    testImplementation selenide
    aspectjWeaverAgent aspectjweaver
}

addCucumberSuite 'test'

cucumber {
    maxParallelForks = getMaxParallelForks()
}

test {
    group = "verification"
    description = "Run cucumber tests in parallel"

    stepDefinitionRoots = ['lv/ss/at/cukes/steps']
    featureRoots = ['lv/ss/at/cukes/features']
    plugins = ['io.qameta.allure.cucumber2jvm.AllureCucumber2Jvm']
    tags = project.hasProperty('tags') ? ["${project.properties.tags} and (not @disabled)"] : ['@all and (not @disabled)']
    systemProperties = ['allure.results.directory': "${project.buildDir}/allure-results"]

    doFirst {
        jvmArgs = ["-javaagent:${configurations.aspectjWeaverAgent.singleFile}"]
    }
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}