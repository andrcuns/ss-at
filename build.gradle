buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:3.16.0"
        classpath "io.qameta.allure:allure-gradle:2.5"
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'io.qameta.allure'

apply from: "${rootDir}/gradle/cuke_reporter.gradle"
apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    testImplementation loggers
    testImplementation cucumber
    testImplementation allure_libs
    testImplementation guice
    testImplementation junit
    testImplementation selenide
    testImplementation courgette_jvm
    aspectjWeaverAgent aspectj_weaver
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

String cucumberTags = project.findProperty('tags') ?: "@all and (not @disabled)"
String javaagent = "-javaagent:${configurations.aspectjWeaverAgent.singleFile}"
List cucumberPlugins = ["json:${cucumberReportDir}/cucumber.json", "io.qameta.allure.cucumber2jvm.AllureCucumber2Jvm"]

task runCukeTests(type: Test) {
    include '**/CukesRunner.class'

    String cucumberOptions = "--tags '${cucumberTags}' ${(cucumberPlugins + 'pretty').collect { "--plugin $it" }.join(" ")}"
    systemProperties = ['cucumber.options': cucumberOptions]
    jvmArgs = [javaagent]

    testLogging {
        showStandardStreams = true
    }

    outputs.upToDateWhen { false }
}

task runCukeTestsInParallel(type: Test) {
    include '**/CukesParallelRunner.class'

    systemProperties = ['cucumber.tags'           : cucumberTags,
                        'cucumber.plugin'         : cucumberPlugins.join(", "),
                        'courgette.vmoptions'     : javaagent,
                        'allure.results.directory': "${project.buildDir}/reports/allure-results"]

    testLogging {
        showStandardStreams = true
    }

    outputs.upToDateWhen { false }
}

afterEvaluate {
    tasks.withType(Test) {
        finalizedBy allureReport, generateCucumberReport
    }
}

allure {
    version = allure_version
    aspectjweaver = false
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}