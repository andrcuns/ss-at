buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:3.16.0"
    }
}

repositories {
    jcenter()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'

apply from: "${rootDir}/gradle/cuke_reporter.gradle"
apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    testImplementation loggers
    testImplementation cucumber
    testImplementation allure_libs
    testImplementation guice
    testImplementation junit
    testImplementation selenide
    testImplementation courgette_jvm
    aspectjWeaverAgent aspectjweaver
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task runCukeTests(type: Test) {
    String cucumberTags = project.findProperty('tags') ?: "@test"
    systemProperties = ['cucumber.tags': cucumberTags, 'courgette.vmoptions': "-javaagent:${configurations.aspectjWeaverAgent.singleFile}"]

    testLogging {
        showStandardStreams = true
    }

    //don't take test task output from cache
    outputs.upToDateWhen { false }
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}