buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:3.16.0"
        classpath "io.qameta.allure:allure-gradle:2.5"
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'io.qameta.allure'

apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    testImplementation loggers
    testImplementation allure_libs
    testImplementation guice
    testImplementation cucumber_guice
    testImplementation selenide
    testImplementation courgette_jvm
    aspectjWeaverAgent aspectj_weaver
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

String cucumberTags = project.findProperty('tags') ?: 'not @disabled'
project.ext.cukeReportDir = ''

task runCukeTests(type: Test) {
    include '**/CukesRunner.class'
    systemProperties = ['cucumber.options': "--tags '${cucumberTags}'"] + System.properties as Map
    doLast { cukeReportDir = "${project.buildDir}/reports/cucumber-report" }
}

task runCukeTestsInParallel(type: Test) {
    include '**/CukesParallelRunner.class'
    systemProperties = ['cucumber.tags': cucumberTags] + System.properties as Map
    doLast { cukeReportDir = "${project.buildDir}/reports/courgette-report/data" }
}

apply from: "${rootDir}/gradle/cuke_reporter.gradle"

tasks.withType(Test) {
    jvmArgs "-javaagent:${configurations.aspectjWeaverAgent.singleFile}"
    systemProperty 'allure.results.directory', "${project.buildDir}/reports/allure-results"

    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }

    afterEvaluate {
        finalizedBy allureReport, generateCucumberReport
    }
}

allure {
    version = allure_version
    aspectjweaver = false
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}