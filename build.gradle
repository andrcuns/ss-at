buildscript {
    repositories {
        maven { url = "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:5.4.0"
        classpath "io.qameta.allure:allure-gradle:2.8.1"
        classpath 'ru.vyarus:gradle-quality-plugin:3.4.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'ru.vyarus.quality'
apply plugin: 'io.qameta.allure'
apply plugin: 'com.github.ben-manes.versions'

apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "11"
    }
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    implementation loggers
    implementation guice
    implementation selenide
    implementation cucumber
    implementation allure_libs
    testImplementation courgette_jvm
    aspectjWeaverAgent aspectj_weaver
}

sourceCompatibility = 11
targetCompatibility = 11

apply from: "${rootDir}/gradle/cuke_reporter.gradle"

String cucumberTags = project.findProperty('tags') ?: 'not @disabled'
String javaagent = "-javaagent:${configurations.aspectjWeaverAgent.singleFile}"

task runCukeTests(type: Test) {
    include '**/CukesRunner.class'
    jvmArgs javaagent
    systemProperties = ['cucumber.options': "--tags '${cucumberTags}'"] + System.properties as Map
    finalizedBy generateSequentialCucumberReport
}

task runCukeTestsInParallel(type: Test) {
    include '**/CukesParallelRunner.class'
    systemProperties = ['cucumber.tags': cucumberTags, 'courgette.vmoptions': javaagent] + System.properties as Map
    finalizedBy generateParallelCucumberReport
}

tasks.withType(Test) {
    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }
    systemProperties.remove("java.endorsed.dirs")

    afterEvaluate { finalizedBy allureReport }
}

allure {
    version = allure_version
    resultsDir = new File("${reportingDir}/allure-results")
    reportDir = new File("${reportingDir}/allure-report")
    aspectjweaver = false
}

quality {
    pmd = false
}