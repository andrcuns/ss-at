buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.qameta.allure:allure-gradle:2.5"
        classpath "net.masterthought:cucumber-reporting:3.16.0"
    }
}

repositories {
    jcenter()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'

apply from: "${rootDir}/gradle/cuke_reporter.gradle"
apply from: "${rootDir}/gradle/allure.gradle"
apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

dependencies {
    testImplementation loggers
    testImplementation cucumber
    testImplementation allure
    testImplementation guice
    testImplementation junit
    testImplementation selenide
    testImplementation courgetteJvm
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task runCukeTests(type: Test) {
    String cucumberTags = project.findProperty('tags') ?: "not @incomplete"
    systemProperties = ['cucumber.tags': cucumberTags]

    testLogging {
        showStandardStreams = true
    }

    //don't take test task output from cache
    outputs.upToDateWhen { false }
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}