buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:3.16.0"
        classpath "io.qameta.allure:allure-gradle:2.5"
    }
}

repositories {
    jcenter()
    mavenLocal()
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'io.qameta.allure'

apply from: "${rootDir}/gradle/cuke_reporter.gradle"
apply from: "${rootDir}/gradle/dependencies.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    testImplementation loggers
    testImplementation cucumber
    testImplementation allure_libs
    testImplementation guice
    testImplementation junit
    testImplementation selenide
    testImplementation courgette_jvm
    aspectjWeaverAgent aspectj_weaver
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task runCukeTestsInParallel(type: Test) {
    include '**/CukesRunner.class'

    String cucumberTags = project.findProperty('tags') ?: "@all, not @disabled"
    systemProperties = ['cucumber.tags'           : cucumberTags,
                        'cucumber.plugin'        : "json:${project.buildDir}/cucumber/cucumber.json, io.qameta.allure.cucumber2jvm.AllureCucumber2Jvm, pretty",
                        'courgette.vmoptions'     : "-javaagent:${configurations.aspectjWeaverAgent.singleFile}",
                        'allure.results.directory': "${project.buildDir}/allure-results"]

    testLogging {
        showStandardStreams = true
    }

    //don't take test task output from cache
    outputs.upToDateWhen { false }
}

allure {
    version = allure_version
    aspectjweaver = false
}

checkstyle {
    toolVersion = '8.3'
    configDir = new File("${rootDir}/gradle/config/checkstyle")
}