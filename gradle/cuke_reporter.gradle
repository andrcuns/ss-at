import net.masterthought.cucumber.ReportBuilder
import net.masterthought.cucumber.Configuration

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:3.16.0"
    }
}

ext {
    cucumberReportDir = "${project.buildDir}/reports/cucumber-report/"
}

task generateCucumberReport {
    description = "Generate cucumber HTML reports"
    doLast {
        def jsonReports = fileTree(dir: cucumberReportDir, include: '**/*.json')
        File reportOutputDirectory = new File(cucumberReportDir.toString())

        List<String> jsonReportFiles = jsonReports.collect { "${it.absolutePath}".toString() } as List<String>

        def configuration = new Configuration(reportOutputDirectory, "ss-at")

        ReportBuilder reportBuilder = new ReportBuilder(jsonReportFiles, configuration)
        reportBuilder.generateReports()
        println "\nReport available on: file://${cucumberReportDir}/cucumber-html-reports/overview-features.html"
    }
}