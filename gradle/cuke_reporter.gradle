import net.masterthought.cucumber.Configuration
import net.masterthought.cucumber.ReportBuilder

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.masterthought:cucumber-reporting:4.6.0"
    }
}

project.ext.reportingDir = "${project.buildDir}/reports"

Map reports = [
        'Sequential': "${reportingDir}/cucumber-report",
        'Parallel'  : "${reportingDir}/courgette-report/data"
]

reports.each { String type, String reportPath ->
    tasks.create("generate${type}CucumberReport") {
        description = "Generate cucumber HTML reports"
        doLast {
            def jsonReports = fileTree(dir: reportPath, include: '**/*.json')
            File reportOutputDirectory = new File(reportingDir)

            List<String> jsonReportFiles = jsonReports.collect { "${it.absolutePath}".toString() } as List<String>

            def configuration = new Configuration(reportOutputDirectory, "ss-at")

            ReportBuilder reportBuilder = new ReportBuilder(jsonReportFiles, configuration)
            reportBuilder.generateReports()
            logger.quiet("Report available on: file://${reportingDir}/cucumber-html-reports/overview-features.html")
            if (type == 'Parallel') {
                logger.quiet("Courgette report: file://${reportingDir}/courgette-report/index.html")
            }
        }
    }
}